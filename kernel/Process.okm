MODULE Process;
    IMPORT Allocator, Debug, FXF, VFS;

    ASM "Process.asm";

    CONST MAX_PROCESSES = 128;
          PROCESS_SIZE = 24;

    CONST STATE_UNUSED = 0;
          STATE_DEAD = 1;
          STATE_RUNNABLE = 2;
          STATE_RUNNING = 3;
          STATE_SLEEPING = 4;

    TYPE Process = RECORD
        id: INT;
        instructionPtr: PTR;
        stackPtr: PTR;
        codeBlockPtr: PTR;
        stackBlockPtr: PTR;
        state: INT;
    END;

    VAR currentProcess: POINTER TO Process;
        processes: ARRAY MAX_PROCESSES OF POINTER TO Process;
        schedulerProcess: Process;

    PROCEDURE InitializeScheduler();
    BEGIN
        schedulerProcess.instructionPtr := PTROF(Scheduler);
        schedulerProcess.stackPtr := Allocate(0FFFFH) + 0FFFFH;
        currentProcess := 0;
    END;

    PROCEDURE NewProcess(
        fileName: POINTER TO CHAR;
        ioPtr: POINTER TO FileOrStream;
        argument: INT;
        table: POINTER TO POINTER TO CHAR;
    ): INT;
    VAR id: INT;
        fileStruct: FileOrStream;
        fileSize: INT;
        instructionPtr: PTR;
        codeBlockPtr: PTR;
        stackPtr: PTR;
        stackBlockPtr: PTR;
        processPtr: POINTER TO Process;
    BEGIN
        (* find an unused ID, or fail if none found *)
        id := FindUnusedProcess();
        IF id = 0FFFFFFFFH THEN
            RETURN(0FFFFFFFFH);
        END;

        (* open the file and get its size *)
        IF Open(fileName, PTROF(fileStruct)) = 0 THEN
            RETURN(0FFFFFFFFH);
        END;
        fileSize := GetSize(PTROF(fileStruct));

        (* allocate a block of memory for the process code *)
        codeBlockPtr := Allocate(fileSize);
        IF codeBlockPtr = 0 THEN
            RETURN(0FFFFFFFFH);
        END;

        (* read the file into the allocated block of memory *)
        Read(fileSize, PTROF(fileStruct), codeBlockPtr);

        (* allocate a 64KB block of memory for the process stack *)
        stackBlockPtr := Allocate(0FFFFH);
        IF stackBlockPtr = 0 THEN
            Free(codeBlockPtr);
            RETURN(0FFFFFFFFH);
        END;
        stackPtr := stackBlockPtr + 0FFFFH;

        (* push the table pointer to the new process's stack *)
        stackPtr := stackPtr - 4;
        PUTINT(stackPtr, table);

        (* push the argument to the new process's stack *)
        stackPtr := stackPtr - 4;
        PUTINT(stackPtr, argument);

        (* push the io pointer to the new process's stack *)
        stackPtr := stackPtr - 4;
        PUTINT(stackPtr, ioPtr);

        (* allocate a block of memory for the process record *)
        processPtr := Allocate(PROCESS_SIZE);
        IF processPtr = 0 THEN
            Free(codeBlockPtr);
            Free(stackBlockPtr);
            RETURN(0FFFFFFFFH);
        END;

        (* relocate the binary *)
        instructionPtr := RelocateFXF(codeBlockPtr);
        IF instructionPtr = 0 THEN
            Free(codeBlockPtr);
            Free(stackBlockPtr);
            Free(processPtr);
            RETURN(0FFFFFFFFH);
        END;

        (* finally, set all of the values in the process record *)
        processPtr^.id := id;
        processPtr^.instructionPtr := instructionPtr;
        processPtr^.stackPtr := stackPtr;
        processPtr^.codeBlockPtr := codeBlockPtr;
        processPtr^.stackBlockPtr := stackBlockPtr;
        processPtr^.state := STATE_RUNNABLE;
        processes[id] := processPtr;

        RETURN(id);
    END;

    PROCEDURE EndProcess(processId: INT;);
    VAR tempProcess: POINTER TO Process;
    BEGIN
        tempProcess := processes[processId];
        IF tempProcess = 0 THEN
            RETURN();
        END;
        (* if we get here then we found a process with this ID, now kill it :3 *)
        (* upon the next round of the scheduler, it will see that this process is *)
        (* dead, and will clean up its memory *)
        tempProcess^.state := STATE_DEAD;
        (* enter the scheduler *)
        YieldProcess();
        (* we should never reach this point, if we do then something is very wrong *)
        Unreachable();
    END;

    PROCEDURE MyProcess(): INT;
    BEGIN
        RETURN(currentProcess^.id);
    END;

    PROCEDURE GetProcess(processId: INT;): POINTER TO Process;
    BEGIN
        RETURN(processes[processId]);
    END;

    EXTERN PROCEDURE YieldProcess: INT;
    EXTERN PROCEDURE SwitchProcess: INT;

    PROCEDURE Scheduler();
    VAR i: INT;
        tempProcess: POINTER TO Process;
    BEGIN
        currentProcess := PTROF(schedulerProcess);
        WHILE 1 DO
            i := 0;
            WHILE i <| MAX_PROCESSES DO
                tempProcess := processes[i];
                IF tempProcess = 0 THEN
                    i := i + 1;
                    CONTINUE();
                END;
                IF tempProcess^.state = STATE_DEAD THEN
                    CleanUpDeadProcess(i);
                    i := i + 1;
                    CONTINUE();
                END;
                IF tempProcess^.state # STATE_RUNNABLE THEN
                    i := i + 1;
                    CONTINUE();
                END;

                (* mark the process as running and switch to it, setting it as the current process *)
                tempProcess^.state := STATE_RUNNING;
                SwitchProcess(tempProcess);

                (* once we reach this point, the process has switched back here *)
                (* mark it as runnable if it is currently running *)
                IF tempProcess^.state = STATE_RUNNING THEN
                    tempProcess^.state := STATE_RUNNABLE;
                END;
                i := i + 1;
            END;
        END;
    END;

    PROCEDURE FindUnusedProcess(): INT;
    VAR i: INT;
    BEGIN
        i := 0;
        WHILE i <| MAX_PROCESSES DO
            IF processes[i] = 0 THEN
                RETURN(i);
            END;
            i := i + 1;
        END;
        RETURN(0FFFFFFFFH);
    END;

    PROCEDURE CleanUpDeadProcess(processId: INT;);
    VAR processPtr: POINTER TO Process;
    BEGIN
        processPtr := processes[processId];
        Free(processPtr^.codeBlockPtr);
        Free(processPtr^.stackBlockPtr);
        Free(processPtr);
        processes[processId] := 0;
    END;
END.
